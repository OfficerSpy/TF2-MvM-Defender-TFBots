#define MAX_RUNTIME_ATTRIBUTES 20

#if !defined __tf_econ_data_included
#define TF_ITEMDEF_DEFAULT	-1
#endif

public const int WEAPONS_SCOUT_PRIMARY[] = {TF_ITEMDEF_DEFAULT, 200, 45, 220, 448, 669, 772, 799, 808, 888, 897, 906, 915, 964, 973, 1078, 1103, 15002, 15015, 15021, 15029, 15036, 15053, 15065, 15069, 15106, 15107, 15108, 15131, 15151, 15157};
public const int WEAPONS_SCOUT_SECONDARY[] = {TF_ITEMDEF_DEFAULT, 209, 46, 160, 163, 222, 294, 449, 773, 812, 833, 1121, 1145, 15013, 15018, 15035, 15041, 15046, 15056, 15060, 15061, 15100, 15101, 15102, 15126, 15148, 30666};
public const int WEAPONS_SCOUT_MELEE[] = {TF_ITEMDEF_DEFAULT, 190, 44, 221, 264, 317, 325, 349, 355, 423, 450, 452, 474, 572, 648, 660, 880, 939, 954, 999, 1013, 1071, 1123, 1127, 30667, 30758};
public const int WEAPONS_SOLDIER_PRIMARY[] = {TF_ITEMDEF_DEFAULT, 205, 127, 228, 414, 441, 513, 658, 730, 800, 809, 889, 898, 907, 916, 965, 974, 1085, 1104, 15006, 15014, 15028, 15043, 15052, 15057, 15081, 15104, 15105, 15129, 15130, 15150};
public const int WEAPONS_SOLDIER_SECONDARY[] = {TF_ITEMDEF_DEFAULT, 199, 129, 133, 226, 354, 415, 442, 444, 1001, 1101, 1141, 1153, 15003, 15016, 15044, 15047, 15085, 15109, 15132, 15133, 15152};
public const int WEAPONS_SOLDIER_MELEE[] = {TF_ITEMDEF_DEFAULT, 196, 128, 154, 264, 357, 416, 423, 447, 474, 775, 880, 939, 954, 1013, 1071, 1123, 1127, 30758};
public const int WEAPONS_PYRO_PRIMARY[] = {TF_ITEMDEF_DEFAULT, 208, 40, 215, 594, 659, 741, 798, 807, 887, 896, 905, 914, 963, 972, 1146, 1178, 15005, 15017, 15030, 15034, 15049, 15054, 15066, 15067, 15068, 15089, 15090, 15115, 15141, 30474};
public const int WEAPONS_PYRO_SECONDARY[] = {TF_ITEMDEF_DEFAULT, 199, 39, 351, 415, 595, 740, 1081, 1141, 1153, 1179, 1180, 15003, 15016, 15044, 15047, 15085, 15109, 15132, 15133, 15152};
public const int WEAPONS_PYRO_MELEE[] = {TF_ITEMDEF_DEFAULT, 192, 38, 153, 214, 264, 326, 348, 423, 457, 466, 474, 593, 739, 813, 834, 880, 939, 954, 1000, 1013, 1071, 1123, 1127, 1181, 30758};
public const int WEAPONS_DEMOMAN_PRIMARY[] = {TF_ITEMDEF_DEFAULT, 206, 308, 405, 608, 996, 1007, 1101, 1151, 15077, 15079, 15091, 15092, 15116, 15117, 15142, 15158};
public const int WEAPONS_DEMOMAN_SECONDARY[] = {TF_ITEMDEF_DEFAULT, 207, 130, 131, 265, 406, 661, 797, 806, 886, 895, 904, 913, 962, 971, 1099, 1144, 1150, 15009, 15012, 15024, 15038, 15045, 15048, 15082, 15083, 15084, 15113, 15137, 15138, 15155};
public const int WEAPONS_DEMOMAN_MELEE[] = {TF_ITEMDEF_DEFAULT, 191, 132, 154, 172, 264, 266, 307, 327, 357, 404, 423, 474, 482, 609, 880, 939, 954, 1013, 1071, 1082, 1123, 1127, 30758};
public const int WEAPONS_HEAVY_PRIMARY[] = {TF_ITEMDEF_DEFAULT, 202, 41, 298, 312, 424, 654, 793, 802, 811, 832, 850, 882, 891, 900, 909, 958, 967, 15004, 15020, 15026, 15031, 15040, 15055, 15086, 15087, 15088, 15098, 15099, 15123, 15124, 15125, 15147};
public const int WEAPONS_HEAVY_SECONDARY[] = {TF_ITEMDEF_DEFAULT, 199, 425, 1141, 1153, 15003, 15016, 15044, 15047, 15085, 15109, 15132, 15133, 15152};
public const int WEAPONS_HEAVY_MELEE[] = {TF_ITEMDEF_DEFAULT, 195, 43, 239, 264, 310, 331, 423, 426, 474, 587, 656, 880, 939, 1013, 1071, 1084, 1100, 1123, 1127, 1184, 30758};
public const int WEAPONS_ENGINEER_PRIMARY[] = {TF_ITEMDEF_DEFAULT, 199, 141, 527, 588, 997, 1004, 1141, 1153, 15003, 15016, 15044, 15047, 15085, 15109, 15132, 15133, 15152};
public const int WEAPONS_ENGINEER_SECONDARY[] = {TF_ITEMDEF_DEFAULT, 209, 140, 160, 294, 528, 1086, 15013, 15018, 15035, 15041, 15046, 15056, 15060, 15061, 15100, 15101, 15102, 15126, 15148, 30666, 30668};
public const int WEAPONS_ENGINEER_MELEE[] = {TF_ITEMDEF_DEFAULT, 197, 142, 155, 169, 329, 423, 589, 662, 795, 804, 884, 893, 902, 911, 960, 969, 1071, 1123, 15073, 15074, 15075, 15139, 15140, 15114, 15156, 30758};
public const int WEAPONS_MEDIC_PRIMARY[] = {TF_ITEMDEF_DEFAULT, 204, 36, 305, 412, 1079};
public const int WEAPONS_MEDIC_SECONDARY[] = {TF_ITEMDEF_DEFAULT, 211, 35, 411, 663, 796, 805, 885, 894, 903, 912, 961, 970, 998, 15008, 15010, 15025, 15039, 15050, 15078, 15097, 15121, 15122, 15120, 15145, 15146};
public const int WEAPONS_MEDIC_MELEE[] = {TF_ITEMDEF_DEFAULT, 198, 37, 173, 264, 304, 413, 423, 474, 880, 939, 954, 1003, 1013, 1071, 1123, 1127, 1143, 30758};
public const int WEAPONS_SNIPER_PRIMARY[] = {TF_ITEMDEF_DEFAULT, 201, 56, 230, 402, 526, 664, 752, 792, 801, 851, 881, 890, 899, 908, 957, 966, 1005, 1092, 1098, 15000, 15007, 15019, 15023, 15033, 15059, 15070, 15071, 15072, 15111, 15112, 15135, 15136, 15154, 30665};
public const int WEAPONS_SNIPER_SECONDARY[] = {TF_ITEMDEF_DEFAULT, 203, 57, 58, 231, 642, 751, 1083, 1105, 1149, 15001, 15022, 15032, 15037, 15058, 15076, 15110, 15134, 15153};
public const int WEAPONS_SNIPER_MELEE[] = {TF_ITEMDEF_DEFAULT, 193, 171, 232, 264, 401, 423, 474, 880, 939, 954, 1013, 1071, 1123, 1127, 30758};
public const int WEAPONS_SPY_SECONDARY[] = {TF_ITEMDEF_DEFAULT, 210, 61, 161, 224, 460, 525, 1006, 1142, 15011, 15027, 15042, 15051, 15062, 15063, 15064, 15103, 15128, 15127, 15149};
public const int WEAPONS_SPY_BUILDING[] = {TF_ITEMDEF_DEFAULT, 736, 810, 831, 933, 1080, 1102};
public const int WEAPONS_SPY_MELEE[] = {TF_ITEMDEF_DEFAULT, 194, 225, 356, 423, 461, 574, 638, 649, 665, 727, 794, 803, 883, 892, 901, 910, 959, 968, 1071, 15080, 15094, 15095, 15096, 15118, 15119, 15143, 15144, 30758};
public const int WEAPONS_SPY_PDA2[] = {TF_ITEMDEF_DEFAULT, 212, 59, 60, 297, 947};

bool g_bHasCustomLoadout[MAXPLAYERS + 1];
bool g_bHasBoughtUpgrades[MAXPLAYERS + 1];

//Each cell is a weapon item definiton index
static int m_iWeaponPrimary[MAXPLAYERS + 1] = {TF_ITEMDEF_DEFAULT, ...};
static int m_iWeaponSecondary[MAXPLAYERS + 1] = {TF_ITEMDEF_DEFAULT, ...};
static int m_iWeaponMelee[MAXPLAYERS + 1] = {TF_ITEMDEF_DEFAULT, ...};
static int m_iWeaponPDA2[MAXPLAYERS + 1] = {TF_ITEMDEF_DEFAULT, ...};

//Each cell is an attribute index
static int m_iAttribPrimary[MAXPLAYERS + 1][MAX_RUNTIME_ATTRIBUTES];
static int m_iAttribSecondary[MAXPLAYERS + 1][MAX_RUNTIME_ATTRIBUTES];
static int m_iAttribMelee[MAXPLAYERS + 1][MAX_RUNTIME_ATTRIBUTES];

//Each cell is an attribute value
static float m_flAttrValPrimary[MAXPLAYERS + 1][MAX_RUNTIME_ATTRIBUTES];
static float m_flAttrValSecondary[MAXPLAYERS + 1][MAX_RUNTIME_ATTRIBUTES];
static float m_flAttrValMelee[MAXPLAYERS + 1][MAX_RUNTIME_ATTRIBUTES];

void LoadLoadoutFunctions()
{
	RegConsoleCmd("sm_redbot_upgraded", Command_BoughtUpgrades);
}

public Action Command_BoughtUpgrades(int client, int args)
{
	//Only need to remember upgrades if using custom loadouts
	if (!redbots_manager_use_custom_loadouts.BoolValue)
		return Plugin_Handled;
	
	//Only our bots should execute this command
	if (!IsFakeClient(client))
		return Plugin_Handled;
	
	//Get current weapons
	int primaryWep = GetPlayerWeaponSlot(client, TFWeaponSlot_Primary);
	int secondaryWep = GetPlayerWeaponSlot(client, TFWeaponSlot_Secondary);
	int meleeWep = GetPlayerWeaponSlot(client, TFWeaponSlot_Melee);
	
	ClearSavedAttributes(client);
	
	int count;
	Address attr;
	
	if (IsValidEntity(primaryWep))
	{		
		count = TF2Attrib_ListDefIndices(primaryWep, m_iAttribPrimary[client]); //Save attribute indexes
		
		if (count > 0)
		{
			for (int i = 0; i < count; i++)
			{
				attr = TF2Attrib_GetByDefIndex(primaryWep, m_iAttribPrimary[client][i]); //Get attribute address
				m_flAttrValPrimary[client][i] = TF2Attrib_GetValue(attr); //Save attribute values
			}
		}
	}
	
	if (IsValidEntity(secondaryWep))
	{		
		count = TF2Attrib_ListDefIndices(secondaryWep, m_iAttribSecondary[client]);
		
		if (count > 0)
		{
			for (int i = 0; i < count; i++)
			{
				attr = TF2Attrib_GetByDefIndex(secondaryWep, m_iAttribSecondary[client][i]);
				m_flAttrValSecondary[client][i] = TF2Attrib_GetValue(attr);
			}
		}
	}
	
	if (IsValidEntity(meleeWep))
	{		
		count = TF2Attrib_ListDefIndices(meleeWep, m_iAttribMelee[client]);
		
		if (count > 0)
		{
			for (int i = 0; i < count; i++)
			{
				attr = TF2Attrib_GetByDefIndex(meleeWep, m_iAttribMelee[client][i]);
				m_flAttrValMelee[client][i] = TF2Attrib_GetValue(attr);
			}
		}
	}
	
	if (redbots_manager_debug.BoolValue)
		PrintToChatAll("[Command_BoughtUpgrades] SAVED WEAPON STATS FOR %N", client);
	
	g_bHasBoughtUpgrades[client] = true;
	
	return Plugin_Handled;
}

public Action Timer_GiveCustomLoadout(Handle timer, int client)
{
	if (!IsClientInGame(client))
		return Plugin_Stop;
	
	//These store weapon entity indexes so we can pass them later
	//PDA2 is excluded as it doesn't currently get upgrades
	int primary = -1, secondary = -1, melee = -1;
	
	char itemClassname[35];
	
	if (m_iWeaponPrimary[client] > TF_ITEMDEF_DEFAULT)
	{
		TF2_RemoveWeaponSlot(client, TFWeaponSlot_Primary);
		
		if (TF2Econ_GetItemClassName(m_iWeaponPrimary[client], itemClassname, sizeof(itemClassname)))
		{
			TF2Econ_TranslateWeaponEntForClass(itemClassname, sizeof(itemClassname), TF2_GetPlayerClass(client));
			primary = GiveItemToPlayer(client, itemClassname, m_iWeaponPrimary[client], 1, 6);
			
			if (g_bHasBoughtUpgrades[client] == false)
			{
				switch (m_iWeaponPrimary[client])
				{
					case 730:	TF2Attrib_SetByName(primary, "auto fires when full", 1.0); //Beggar's Bazooka: prevent overloading
					case 996:	TF2Attrib_SetByName(primary, "grenade launcher mortar mode", 0.0); //Loose Cannon: prevent charging
				}
			}
		}
		else
		{
			LogError("Timer_GiveCustomLoadout: Could not add primary %d to %N!", m_iWeaponPrimary[client], client);
		}
	}
	
	if (m_iWeaponSecondary[client] > TF_ITEMDEF_DEFAULT && !TF2_IsShieldEquipped(client))
	{
		TF2_RemoveWeaponSlot(client, TFWeaponSlot_Secondary);
		
		if (TF2Econ_GetItemClassName(m_iWeaponSecondary[client], itemClassname, sizeof(itemClassname)))
		{
			TF2Econ_TranslateWeaponEntForClass(itemClassname, sizeof(itemClassname), TF2_GetPlayerClass(client));
			secondary = GiveItemToPlayer(client, itemClassname, m_iWeaponSecondary[client], 1, 6);
			
			if (g_bHasBoughtUpgrades[client] == false && StrEqual(itemClassname, "tf_weapon_pipebomblauncher"))
				TF2Attrib_SetByName(secondary, "stickybomb charge rate", 0.0); //Instant fire stickies
		}
		else
		{
			LogError("Timer_GiveCustomLoadout: Could not add secondary %d to %N!", m_iWeaponSecondary[client], client);
		}
	}
	
	if (m_iWeaponMelee[client] > TF_ITEMDEF_DEFAULT)
	{
		TF2_RemoveWeaponSlot(client, TFWeaponSlot_Melee);
		
		if (TF2Econ_GetItemClassName(m_iWeaponMelee[client], itemClassname, sizeof(itemClassname)))
		{
			TF2Econ_TranslateWeaponEntForClass(itemClassname, sizeof(itemClassname), TF2_GetPlayerClass(client));
			melee = GiveItemToPlayer(client, itemClassname, m_iWeaponMelee[client], 1, 6);
			
			switch (m_iWeaponMelee[client])
			{
				case 1071:
				{
					//Dumb way to check, but we don't want to change these attributes every spawn
					if (g_bHasBoughtUpgrades[client] == false)
						GiveGoldPanStats(melee);
				}
			}
		}
		else
		{
			LogError("Timer_GiveCustomLoadout: Could not add melee %d to %N!", m_iWeaponMelee[client], client);
		}
	}
	
	if (m_iWeaponPDA2[client] > TF_ITEMDEF_DEFAULT)
	{
		TF2_RemoveWeaponSlot(client, TFWeaponSlot_Building);
		
		if (TF2Econ_GetItemClassName(m_iWeaponPDA2[client], itemClassname, sizeof(itemClassname)))
			GiveItemToPlayer(client, itemClassname, m_iWeaponPDA2[client], 1, 6);
		else
			LogError("Timer_GiveCustomLoadout: Could not add pda2 %d to %N!", m_iWeaponPDA2[client], client);
	}
	
	if (g_bHasBoughtUpgrades[client])
		ReapplyItemUpgrades(client, primary, secondary, melee);
	
	/* Certain weapons or upgrades may have changed our health and ammo
	so we must refill them completely to the max, though health
	may get reduced if max health was lowered by a weapon's attribute */
	for (int i = TF_AMMO_PRIMARY; i < TF_AMMO_COUNT; i++)
		GivePlayerAmmo(client, 1000, i, true);
	
	//For players, this is calculated max health
	int maxHealth = TF2Util_GetEntityMaxHealth(client);
	
	if (GetClientHealth(client) != maxHealth)
	{
		BaseEntity_SetMaxHealth(client, maxHealth);
		SetEntityHealth(client, maxHealth);
	}
	
	PostInventoryApplication(client);
	
	if (redbots_manager_debug.BoolValue)
		PrintToChatAll("[Timer_GiveCustomLoadout] %N's ammo: %d/%d", client, BaseCombatCharacter_GetAmmoCount(client, TF_AMMO_PRIMARY), TF2Util_GetPlayerMaxAmmo(client, TF_AMMO_PRIMARY));
	
	return Plugin_Stop;
}

void ClearSavedAttributes(int client)
{
	for (int i = 0; i < MAX_RUNTIME_ATTRIBUTES; i++)
	{
		m_iAttribPrimary[client][i] = 0;
		m_iAttribSecondary[client][i] = 0;
		m_iAttribMelee[client][i] = 0;
		m_flAttrValPrimary[client][i] = 0.0;
		m_flAttrValSecondary[client][i] = 0.0;
		m_flAttrValMelee[client][i] = 0.0;
	}
}

void PrepareCustomLoadout(int client)
{
	switch(TF2_GetPlayerClass(client))
	{
		case TFClass_Scout:
		{
			m_iWeaponPrimary[client] = GetPreferredWeaponForClass("scout", "primary");
			m_iWeaponSecondary[client] = GetPreferredWeaponForClass("scout", "secondary");
			m_iWeaponMelee[client] = GetPreferredWeaponForClass("scout", "melee");
		}
		case TFClass_Soldier:
		{
			m_iWeaponPrimary[client] = GetPreferredWeaponForClass("soldier", "primary");
			m_iWeaponSecondary[client] = GetPreferredWeaponForClass("soldier", "secondary");
			m_iWeaponMelee[client] = GetPreferredWeaponForClass("soldier", "melee");
		}
		case TFClass_Pyro:
		{
			m_iWeaponPrimary[client] = GetPreferredWeaponForClass("pyro", "primary");
			m_iWeaponSecondary[client] = GetPreferredWeaponForClass("pyro", "secondary");
			m_iWeaponMelee[client] = GetPreferredWeaponForClass("pyro", "melee");
		}
		case TFClass_DemoMan:
		{
			m_iWeaponPrimary[client] = GetPreferredWeaponForClass("demoman", "primary");
			m_iWeaponSecondary[client] = GetPreferredWeaponForClass("demoman", "secondary");
			m_iWeaponMelee[client] = GetPreferredWeaponForClass("demoman", "melee");
		}
		case TFClass_Heavy:
		{
			m_iWeaponPrimary[client] = GetPreferredWeaponForClass("heavyweapons", "primary");
			m_iWeaponSecondary[client] = GetPreferredWeaponForClass("heavyweapons", "secondary");
			m_iWeaponMelee[client] = GetPreferredWeaponForClass("heavyweapons", "melee");
		}
		case TFClass_Engineer:
		{
			m_iWeaponPrimary[client] = GetPreferredWeaponForClass("engineer", "primary");
			m_iWeaponSecondary[client] = GetPreferredWeaponForClass("engineer", "secondary");
			m_iWeaponMelee[client] = GetPreferredWeaponForClass("engineer", "melee");
		}
		case TFClass_Medic:
		{
			m_iWeaponPrimary[client] = GetPreferredWeaponForClass("medic", "primary");
			m_iWeaponSecondary[client] = GetPreferredWeaponForClass("medic", "secondary");
			m_iWeaponMelee[client] = GetPreferredWeaponForClass("medic", "melee");
		}
		case TFClass_Sniper:
		{
			m_iWeaponPrimary[client] = GetPreferredWeaponForClass("sniper", "primary");
			m_iWeaponSecondary[client] = GetPreferredWeaponForClass("sniper", "secondary");
			m_iWeaponMelee[client] = GetPreferredWeaponForClass("sniper", "melee");
		}
		case TFClass_Spy:
		{
			m_iWeaponPrimary[client] = GetPreferredWeaponForClass("spy", "primary");
			m_iWeaponSecondary[client] = GetPreferredWeaponForClass("spy", "secondary");
			m_iWeaponMelee[client] = GetPreferredWeaponForClass("spy", "melee");
			m_iWeaponPDA2[client] = GetPreferredWeaponForClass("spy", "pda2");
		}
	}
	
	char itemClassname[PLATFORM_MAX_PATH];
	
	if (TF2Econ_GetItemClassName(m_iWeaponPrimary[client], itemClassname, sizeof(itemClassname)))
	{
		if (StrEqual(itemClassname, "tf_weapon_sniperrifle") || StrEqual(itemClassname, "tf_weapon_sniperrifle_decap") || StrEqual(itemClassname, "tf_weapon_sniperrifle_classic"))
		{
			//We determined we're using a sniper rifle, so use sniper ai
			SetMission(client, CTFBot_MISSION_SNIPER);
		}
	}
	
	g_bHasCustomLoadout[client] = true;
}

void ResetLoadouts(int client)
{
	g_bHasCustomLoadout[client] = false;
	m_iWeaponPrimary[client] = TF_ITEMDEF_DEFAULT;
	m_iWeaponSecondary[client] = TF_ITEMDEF_DEFAULT;
	m_iWeaponMelee[client] = TF_ITEMDEF_DEFAULT;
	m_iWeaponPDA2[client] = TF_ITEMDEF_DEFAULT;
}

void ReapplyItemUpgrades(int client, int primary, int secondary, int melee)
{
	int i;
	if (IsValidEntity(primary))
	{
		for (i = 0; i < MAX_RUNTIME_ATTRIBUTES; i++)
		{
			//If we hit 0, the rest of the array is probably the same
			if (m_iAttribPrimary[client][i] == 0)
				break;
						
			TF2Attrib_SetByDefIndex(primary, m_iAttribPrimary[client][i], m_flAttrValPrimary[client][i]);
		}
	}
	
	if (IsValidEntity(secondary))
	{
		for (i = 0; i < MAX_RUNTIME_ATTRIBUTES; i++)
		{
			if (m_iAttribSecondary[client][i] == 0)
				break;
			
			TF2Attrib_SetByDefIndex(secondary, m_iAttribSecondary[client][i], m_flAttrValSecondary[client][i]);
		}
	}
	
	if (IsValidEntity(melee))
	{
		for (i = 0; i < MAX_RUNTIME_ATTRIBUTES; i++)
		{
			if (m_iAttribMelee[client][i] == 0)
				break;
			
			TF2Attrib_SetByDefIndex(melee, m_iAttribMelee[client][i], m_flAttrValMelee[client][i]);
		}
	}	
}

//Return weapon def index
int GetRandomWeaponForClass(const char[] class, const char[] slot)
{
	if (StrEqual(class, "scout", false))
	{
		if (StrEqual(slot, "primary", false))
			return WEAPONS_SCOUT_PRIMARY[GetRandomInt(0, sizeof(WEAPONS_SCOUT_PRIMARY) - 1)];
		else if (StrEqual(slot, "secondary", false))
			return WEAPONS_SCOUT_SECONDARY[GetRandomInt(0, sizeof(WEAPONS_SCOUT_SECONDARY) - 1)];
		else if (StrEqual(slot, "melee", false))
			return WEAPONS_SCOUT_MELEE[GetRandomInt(0, sizeof(WEAPONS_SCOUT_MELEE) - 1)];
	}
	else if (StrEqual(class, "soldier", false))
	{
		if (StrEqual(slot, "primary", false))
			return WEAPONS_SOLDIER_PRIMARY[GetRandomInt(0, sizeof(WEAPONS_SOLDIER_PRIMARY) - 1)];
		else if (StrEqual(slot, "secondary", false))
			return WEAPONS_SOLDIER_SECONDARY[GetRandomInt(0, sizeof(WEAPONS_SOLDIER_SECONDARY) - 1)];
		else if (StrEqual(slot, "melee", false))
			return WEAPONS_SOLDIER_MELEE[GetRandomInt(0, sizeof(WEAPONS_SOLDIER_MELEE) - 1)];
	}
	else if (StrEqual(class, "pyro", false))
	{
		if (StrEqual(slot, "primary", false))
			return WEAPONS_PYRO_PRIMARY[GetRandomInt(0, sizeof(WEAPONS_PYRO_PRIMARY) - 1)];
		else if (StrEqual(slot, "secondary", false))
			return WEAPONS_PYRO_SECONDARY[GetRandomInt(0, sizeof(WEAPONS_PYRO_SECONDARY) - 1)];
		else if (StrEqual(slot, "melee", false))
			return WEAPONS_PYRO_MELEE[GetRandomInt(0, sizeof(WEAPONS_PYRO_MELEE) - 1)];
	}
	else if (StrEqual(class, "demoman", false))
	{
		if (StrEqual(slot, "primary", false))
			return WEAPONS_DEMOMAN_PRIMARY[GetRandomInt(0, sizeof(WEAPONS_DEMOMAN_PRIMARY) - 1)];
		else if (StrEqual(slot, "secondary", false))
			return WEAPONS_DEMOMAN_SECONDARY[GetRandomInt(0, sizeof(WEAPONS_DEMOMAN_SECONDARY) - 1)];
		else if (StrEqual(slot, "melee", false))
			return WEAPONS_DEMOMAN_MELEE[GetRandomInt(0, sizeof(WEAPONS_DEMOMAN_MELEE) - 1)];
	}
	else if (StrEqual(class, "heavyweapons", false))
	{
		if (StrEqual(slot, "primary", false))
			return WEAPONS_HEAVY_PRIMARY[GetRandomInt(0, sizeof(WEAPONS_HEAVY_PRIMARY) - 1)];
		else if (StrEqual(slot, "secondary", false))
			return WEAPONS_HEAVY_SECONDARY[GetRandomInt(0, sizeof(WEAPONS_HEAVY_SECONDARY) - 1)];
		else if (StrEqual(slot, "melee", false))
			return WEAPONS_HEAVY_MELEE[GetRandomInt(0, sizeof(WEAPONS_HEAVY_MELEE) - 1)];
	}
	else if (StrEqual(class, "engineer", false))
	{
		if (StrEqual(slot, "primary", false))
			return WEAPONS_ENGINEER_PRIMARY[GetRandomInt(0, sizeof(WEAPONS_ENGINEER_PRIMARY) - 1)];
		else if (StrEqual(slot, "secondary", false))
			return WEAPONS_ENGINEER_SECONDARY[GetRandomInt(0, sizeof(WEAPONS_ENGINEER_SECONDARY) - 1)];
		else if (StrEqual(slot, "melee", false))
			return WEAPONS_ENGINEER_MELEE[GetRandomInt(0, sizeof(WEAPONS_ENGINEER_MELEE) - 1)];
	}
	else if (StrEqual(class, "medic", false))
	{
		if (StrEqual(slot, "primary", false))
			return WEAPONS_MEDIC_PRIMARY[GetRandomInt(0, sizeof(WEAPONS_MEDIC_PRIMARY) - 1)];
		else if (StrEqual(slot, "secondary", false))
			return WEAPONS_MEDIC_SECONDARY[GetRandomInt(0, sizeof(WEAPONS_MEDIC_SECONDARY) - 1)];
		else if (StrEqual(slot, "melee", false))
			return WEAPONS_MEDIC_MELEE[GetRandomInt(0, sizeof(WEAPONS_MEDIC_MELEE) - 1)];
	}
	else if (StrEqual(class, "sniper", false))
	{
		if (StrEqual(slot, "primary", false))
			return WEAPONS_SNIPER_PRIMARY[GetRandomInt(0, sizeof(WEAPONS_SNIPER_PRIMARY) - 1)];
		else if (StrEqual(slot, "secondary", false))
			return WEAPONS_SNIPER_SECONDARY[GetRandomInt(0, sizeof(WEAPONS_SNIPER_SECONDARY) - 1)];
		else if (StrEqual(slot, "melee", false))
			return WEAPONS_SNIPER_MELEE[GetRandomInt(0, sizeof(WEAPONS_SNIPER_MELEE) - 1)];
	}
	else if (StrEqual(class, "spy", false))
	{
		if (StrEqual(slot, "primary", false))
			return WEAPONS_SPY_SECONDARY[GetRandomInt(0, sizeof(WEAPONS_SPY_SECONDARY) - 1)];
		else if (StrEqual(slot, "secondary", false))
			return WEAPONS_SPY_BUILDING[GetRandomInt(0, sizeof(WEAPONS_SPY_BUILDING) - 1)];
		else if (StrEqual(slot, "melee", false))
			return WEAPONS_SPY_MELEE[GetRandomInt(0, sizeof(WEAPONS_SPY_MELEE) - 1)];
		else if (StrEqual(slot, "pda2", false))
			return WEAPONS_SPY_PDA2[GetRandomInt(0, sizeof(WEAPONS_SPY_PDA2) - 1)];
	}
	else
	{
		PrintToChatAll("[GetRandomWeaponForClass] Unknown class of %s", class);
		LogError("GetRandomWeaponForClass: Unknown class %s", class);
	}
	
	return -1;
}

void GiveGoldPanStats(int weapon)
{
	//These may need to become arrays if the effect indexes update in the future
	int sheen = GetRandomInt(1, 7);
	int killstreaker = GetRandomInt(2002, 2008);
	
	TF2Attrib_SetByName(weapon, "item style override", 0.0);
	TF2Attrib_SetByName(weapon, "killstreak tier", 3.0);
	TF2Attrib_SetByName(weapon, "killstreak idleeffect", float(sheen));
	TF2Attrib_SetByName(weapon, "killstreak effect", float(killstreaker));	
}